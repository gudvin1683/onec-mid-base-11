#Область ПрограммныйИнтерфейс 

//Добавление реквизита программно на форму документа
Процедура ДополнитьФорму(Форма) Экспорт
	
	// ++Гудилин Задача____ 20.02.2025
	Имяформы = Форма.Имяформы;
	
	Если Имяформы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
		ДобавитьПолеСогласованнаяСкидкаВГруппаШапкаЛево(Форма);
		ДобавитьКнопкуПересчитатьТаблицуВГруппаШапкаЛево(Форма);
	ИначеЕсли Имяформы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
	ИначеЕсли Имяформы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
	ИначеЕсли Имяформы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда
		ВставитьПолеКонтактноеЛицоНаформуПередСуммаДокумента(Форма);
	ИначеЕсли Имяформы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда
		ВставитьПолеКонтактноеЛицоНаформуПередСуммаДокумента(Форма);
	КонецЕсли;
	// --Гудилин Задача____ 20.02.2025 
	
КонецПроцедуры 

#Конецобласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма) Экспорт
	
	// ++Гудилин Задача11-3 20.02.2025
	ПолеВвода = Форма.Элементы.Добавить("КонтактноеЛицо", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаПраво);
	ПолеВвода.Вид = ВидПоляформы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.ДП_КонтактноеЛицо";
	// --Гудилин Задача11-3 20.02.2025

КонецПроцедуры

Процедура ВставитьПолеКонтактноеЛицоНаформуПередСуммаДокумента(Форма) Экспорт
	
	// ++Гудилин Задача11-3 20.02.2025
	ПолеВвода = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"), , Форма.Элементы.СуммаДокумента);
	ПолеВвода.Вид = ВидПоляформы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.ДП_КонтактноеЛицо";
	// --Гудилин Задача11-3 20.02.2025

КонецПроцедуры

Процедура ДобавитьПолеСогласованнаяСкидкаВГруппаШапкаЛево(Форма) Экспорт
	
	// ++Гудилин Задача11-3 20.02.2025
	Группа = Форма.Элементы.Добавить("ГруппаСкидка", Тип("ГруппаФормы"), Форма.Элементы.ГруппаШапкаЛево);
	Группа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	
	ПолеВвода = Форма.Элементы.Добавить("СогласованнаяСкидка", Тип("ПолеФормы"), Группа);
	ПолеВвода.Вид = ВидПоляформы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.ДП_СогласованнаяСкидка";
	ПолеВвода.УстановитьДействие("ПриИзменении", "ПриИзмененииДП_СогласованнаяСкидка"); 
    // --Гудилин Задача11-3 20.02.2025 
	
КонецПроцедуры 

Процедура ДобавитьКнопкуПересчитатьТаблицуВГруппаШапкаЛево(Форма) Экспорт
		
	// ++Гудилин Задача11-3 20.02.2025	
	Команда = Форма.Команды.Добавить("ПересчитатьТаблицу");
	Команда.Заголовок = "Пересчитать таблицу";
	Команда.Действие = "КомандаПересчитатьТаблицу";
	
	КнопкаФормы = Форма.Элементы.Добавить("КнопкаПересчитатьТаблицу", Тип("КнопкаФормы"), Форма.Элементы.ГруппаСкидка);
	КнопкаФормы.ИмяКоманды = "ПересчитатьТаблицу";
	КнопкаФормы.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
	КнопкаФормы.Картинка = БиблиотекаКартинок.Обновить;
    КнопкаФормы.Отображение = ОтображениеКнопки.КартинкаИТекст;
	КнопкаФормы.ПоложениеКартинки = ПоложениеКартинкиКнопкиФормы.Лево;
    // --Гудилин Задача11-3 20.02.2025 
	
КонецПроцедуры

#Конецобласти